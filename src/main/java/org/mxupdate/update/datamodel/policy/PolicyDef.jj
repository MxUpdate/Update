/*
 *  This file is part of MxUpdate <http://www.mxupdate.org>.
 *
 *  MxUpdate is a deployment tool for a PLM platform to handle
 *  administration objects as single update files (configuration item).
 *
 *  Copyright (C) 2008-2016 The MxUpdate Team - All Rights Reserved
 *
 *  You may use, distribute and modify MxUpdate under the terms of the
 *  MxUpdate license. You should have received a copy of the MxUpdate
 *  license with this file. If not, please write to <info@mxupdate.org>,
 *  or visit <www.mxupdate.org>.
 *
 */

options {
    JDK_VERSION = "1.6";
    STATIC = false;
    ERROR_REPORTING = true;
    USER_TOKEN_MANAGER = false;
//  USER_CHAR_STREAM = true;
//  OPTIMIZE_TOKEN_MANAGER = true;
    SUPPORT_CLASS_VISIBILITY_PUBLIC = false;
    LOOKAHEAD = 1;
//    DEBUG_PARSER = true;
}

PARSER_BEGIN(PolicyDefParser)

package org.mxupdate.update.datamodel.policy;

import java.lang.reflect.InvocationTargetException;
import java.util.Arrays;
import java.util.Collection;
import java.util.HashSet;
import java.util.Map;
import java.util.Set;

import org.mxupdate.mapping.TypeDef_mxJPO;
import org.mxupdate.update.datamodel.helper.TriggerList_mxJPO.Trigger;
import org.mxupdate.update.datamodel.Policy_mxJPO;
import org.mxupdate.update.datamodel.Policy_mxJPO.State;
import org.mxupdate.update.datamodel.helper.AccessList_mxJPO;
import org.mxupdate.update.datamodel.helper.AccessList_mxJPO.Access;
import org.mxupdate.update.datamodel.helper.AccessList_mxJPO.Prefix;
import org.mxupdate.update.util.AbstractParser_mxJPO;
import org.mxupdate.update.util.AdminPropertyList_mxJPO.AdminProperty;
import org.mxupdate.update.util.ParameterCache_mxJPO;

public class PolicyDefParser
    extends AbstractParser_mxJPO
{
}
PARSER_END(PolicyDefParser)

<DEFAULT> SKIP :
{
  " "
| "\t"
| "\n"
| "\r"
}

<DEFAULT> TOKEN : /** first level */
{
    <DESCRIPTION:     "description">      : S_EXPECTED
  | <TYPE:            "type">             : LALL_EXPECTED
  | <FORMAT:          "format">           : LALL_EXPECTED
  | <STORE:           "store">            : S_EXPECTED
  | <DEFAULTFORMAT:   "defaultformat">    : S_EXPECTED
  | <ENFORCE:         "enforce">          : B_EXPECTED
  | <DELIMITER:       "delimiter">        : D_EXPECTED
  | <SEQUENCE:        "sequence">         : S_EXPECTED
  | <MINORSEQUENCE:   "minorsequence">    : S_EXPECTED
  | <MAJORSEQUENCE:   "majorsequence">    : S_EXPECTED
  | <HIDDEN:          "hidden">           : B_EXPECTED
  | <STATE:           "state">            : S_EXPECTED
  | <ALLSTATE:        "allstate">         : DEFAULT
}

<DEFAULT> TOKEN : /** State and depending signature and trigger Tokens */
{
      <REGISTEREDNAME:                      "registeredName">                                       : S_EXPECTED
    | <REVISION:                            "revision">                                             : B_EXPECTED
    | <MINORREVISION:                       "minorrevision">                                        : B_EXPECTED
    | <MAJORREVISION:                       "majorrevision">                                        : B_EXPECTED
    | <VERSION:                             "version">                                              : B_EXPECTED
    | <PROMOTE:                             "promote">                                              : B_EXPECTED
    | <CHECKOUTHISTORY:                     "checkouthistory">                                      : B_EXPECTED
    | <ENFORCERESERVEACCESS:                "enforcereserveaccess">                                 : DEFAULT
    | <ENFORCERESERVEACCESS_TRUE:           "enforcereserveaccess" ([" ","\t","\n","\r"])+ (["t","T"] ["r","R"] ["u","U"] ["e","E"] | "\"" ["t","T"] ["r","R"] ["u","U"] ["e","E"] "\"")>
                                                                                                    : DEFAULT
    | <ENFORCERESERVEACCESS_NOT:            "notenforcereserveaccess">                              : DEFAULT
    | <ENFORCERESERVEACCESS_EXCLAMATION:    "!enforcereserveaccess">                                : DEFAULT
    | <ENFORCERESERVEACCESS_FALSE:          "enforcereserveaccess" ([" ","\t","\n","\r"])+ (["f","F"] ["a","A"] ["l","L"] ["s","S"] ["e","E"] | "\"" ["f","F"] ["a","A"] ["l","L"] ["s","S"] ["e","E"] "\"")>
                                                                                                    : DEFAULT
    | <PUBLISHED:                           "published">                                            : B_EXPECTED
    | <REVOKE:                              "revoke">                                               : DEFAULT
    | <LOGIN:                               "login">                                                : DEFAULT
    | <OWNER:                               "owner">                                                : STATEACCESS_EXPECTED
    | <PUBLIC:                              "public">                                               : STATEACCESS_EXPECTED
    | <USER:                                "user">                                                 : STATEACCESS_STRING_EXPECTED
    | <ACTION:                              "action">                                               : S_EXPECTED
    | <CHECK:                               "check">                                                : S_EXPECTED
    | <OVERRIDE:                            "override">                                             : S_EXPECTED
    | <INPUT:                               "input">                                                : S_EXPECTED
    | <TRIGGER:                             "trigger">                                              : S_EXPECTED
    | <APPROVE:                             "approve">                                              : L_EXPECTED
    | <IGNORE:                              "ignore">                                               : L_EXPECTED
    | <REJECT:                              "reject">                                               : L_EXPECTED
    | <BRANCH:                              "branch">                                               : S_EXPECTED
    | <SIGNATURE:                           "signature">                                            : S_EXPECTED
    | <ROUTE:                               "route">                                                : LS_EXPECTED
}

/************************************************************** State Access */
<STATEACCESS_EXPECTED> SKIP :
{
  " "
| "\t"
| "\n"
| "\r"
}

<STATEACCESS_EXPECTED> TOKEN:
{
      <KEY:                                 "key" >                                                 : STATEACCESS_STRING_EXPECTED
    | <ACCESS:                              "{" ([" ","\t","\n","\r"])* (["a"-"z"]) (["a"-"z"," ","\t","\n","\r"])* "}">
                                                                                                    : DEFAULT
}
/** String within State Access */
<STATEACCESS_STRING_EXPECTED> SKIP :
{
  " " | "\t" | "\n" | "\r"
}

<STATEACCESS_STRING_EXPECTED> TOKEN :
{
      <STATEACCESS_STRING : (<STATEACCESS_STRING_APOSTROPHE>(<STATEACCESS_STRING_CHAR> | "\\\"" | "\\{" | "\\}" | " ")*<STATEACCESS_STRING_APOSTROPHE>)>
                                                                                                    : STATEACCESS_EXPECTED
    | <STATEACCESS_SINGLE_STRING : (<STATEACCESS_STRING_CHAR>)+>                                    : STATEACCESS_EXPECTED
    | <#STATEACCESS_STRING_APOSTROPHE: "\"" >
    | <#STATEACCESS_STRING_CHAR : ~["\u0000"-"\u0020","\"","{","}","\u0100"-"\uffff"]>
}

/** After access definition back to default */
<DEFAULT> TOKEN:
{      <FILTER:                              "filter">                                               : S_EXPECTED
    | <LOCALFILTER:                         "localfilter">                                          : S_EXPECTED
    | <ORGANIZATION_ANY:                    "any"         ([" ","\t","\n","\r"])+ "organization">   : DEFAULT
    | <ORGANIZATION_SINGLE:                 "single"      ([" ","\t","\n","\r"])+ "organization">   : DEFAULT
    | <ORGANIZATION_ANCESTOR:               "ancestor"    ([" ","\t","\n","\r"])+ "organization">   : DEFAULT
    | <ORGANIZATION_DESCENDANT:             "descendant"  ([" ","\t","\n","\r"])+ "organization">   : DEFAULT
    | <ORGANIZATION_RELATED:                "related"     ([" ","\t","\n","\r"])+ "organization">   : DEFAULT
    | <PROJECT_ANY:                         "any"         ([" ","\t","\n","\r"])+ "project">        : DEFAULT
    | <PROJECT_SINGLE:                      "single"      ([" ","\t","\n","\r"])+ "project">        : DEFAULT
    | <PROJECT_ANCESTOR:                    "ancestor"    ([" ","\t","\n","\r"])+ "project">        : DEFAULT
    | <PROJECT_DESCENDANT:                  "descendant"  ([" ","\t","\n","\r"])+ "project">        : DEFAULT
    | <PROJECT_RELATED:                     "related"     ([" ","\t","\n","\r"])+ "project">        : DEFAULT
    | <OWNER_ANY:                           "any"         ([" ","\t","\n","\r"])+ "owner">          : DEFAULT
    | <OWNER_CONTEXT:                       "context"     ([" ","\t","\n","\r"])+ "owner">          : DEFAULT
    | <RESERVE_ANY:                         "any"         ([" ","\t","\n","\r"])+ "reserve">        : DEFAULT
    | <RESERVE_CONTEXT:                     "context"     ([" ","\t","\n","\r"])+ "reserve">        : DEFAULT
    | <RESERVE_NO:                          "no"          ([" ","\t","\n","\r"])+ "reserve">        : DEFAULT
    | <RESERVE_INCLUSIVE:                   "inclusive"   ([" ","\t","\n","\r"])+ "reserve">        : DEFAULT
    | <MATURITY_ANY:                        "any"         ([" ","\t","\n","\r"])+ "maturity">       : DEFAULT
    | <MATURITY_NO:                         "no"          ([" ","\t","\n","\r"])+ "maturity">       : DEFAULT
    | <MATURITY_PUBLIC:                     "public"      ([" ","\t","\n","\r"])+ "maturity">       : DEFAULT
    | <MATURITY_PROTECTED:                  "protected"   ([" ","\t","\n","\r"])+ "maturity">       : DEFAULT
    | <MATURITY_PRIVATE:                    "private"     ([" ","\t","\n","\r"])+ "maturity">       : DEFAULT
    | <MATURITY_NOTPRIVATE:                 "notprivate"  ([" ","\t","\n","\r"])+ "maturity">       : DEFAULT
    | <MATURITY_PPP:                        "ppp"         ([" ","\t","\n","\r"])+ "maturity">       : DEFAULT
    | <CATEGORY_ANY:                        "any"         ([" ","\t","\n","\r"])+ "category">       : DEFAULT
    | <CATEGORY_OEM:                        "oem"         ([" ","\t","\n","\r"])+ "category">       : DEFAULT
    | <CATEGORY_GOLDPARTNER:                "goldpartner" ([" ","\t","\n","\r"])+ "category">       : DEFAULT
    | <CATEGORY_PARTNER:                    "partner"     ([" ","\t","\n","\r"])+ "category">       : DEFAULT
    | <CATEGORY_SUPPLIER:                   "supplier"    ([" ","\t","\n","\r"])+ "category">       : DEFAULT
    | <CATEGORY_CUSTOMER:                   "customer"    ([" ","\t","\n","\r"])+ "category">       : DEFAULT
    | <CATEGORY_CONTRACTOR:                 "contractor"  ([" ","\t","\n","\r"])+ "category">       : DEFAULT
}

/************************************************************* State Property */
<DEFAULT> TOKEN :
{      <STATE_PROPERTY:                      "property">                                             : S_EXPECTED
    | <STATE_PROPERTYVAL:                   "value">                                                : S_EXPECTED
    | <STATE_PROPERTYTO:                    "to">                                                   : STATE_PROPERTYTO_EXPECTED
}

<STATE_PROPERTYTO_EXPECTED> SKIP :
{
  " " | "\t" | "\n" | "\r"
}

<STATE_PROPERTYTO_EXPECTED> TOKEN :
{
      <STATE_PROPERTYTOTYPE : (<SS_CHAR>)+> : S_EXPECTED
    | <#SS_CHAR : ~["\u0000"-"\u0020","\"","{","}","\u0100"-"\uffff"]>
}


/*************************************************** Boolean for Hidden Value */
<B_EXPECTED> SKIP :
{
  " " | "\t" | "\n" | "\r"
}

<B_EXPECTED> TOKEN :
{
      <BOOLEAN_TRUE : (<BOOLEAN_TRUESTR> | "\"" <BOOLEAN_TRUESTR> "\"")> : DEFAULT
    | <BOOLEAN_FALSE : (<BOOLEAN_FALSESTR> | "\"" <BOOLEAN_FALSESTR> "\"")> : DEFAULT
    | <#BOOLEAN_TRUESTR : ["t","T"] ["r","R"] ["u","U"] ["e","E"]>
    | <#BOOLEAN_FALSESTR : ["f","F"] ["a","A"] ["l","L"] ["s","S"] ["e","E"]>
}

/*************************************************** Delimiter Chacter */
<D_EXPECTED> SKIP :
{
  " " | "\t" | "\n" | "\r"
}

<D_EXPECTED> TOKEN :
{
      <DELIMITER_CHAR : (<DELIMITER_CHARSTR> | "\"" <DELIMITER_CHARSTR> "\"")>  : DEFAULT
    | <#DELIMITER_CHARSTR : ~["\u0000"-"\u0020","0"-"9","a"-"z","A"-"Z","?","*","'","\"","[","]","(",")"]>
}

/************************************************************* List -> String */
<LS_EXPECTED> SKIP:
{
  " " | "\t" | "\n" | "\r"
}

<LS_EXPECTED> TOKEN : /* List of Strings */
{
      <LS_CURLY_BRACKET_OPEN : "{">    : LS_EXPECTED
    | <LS_CURLY_BRACKET_CLOSE : "}">   : S_EXPECTED
    | <LS_STRING : (<LS_APOSTROPHE>(<LS_CHAR> | "\\\"" | "\\{" | "\\}" | " ")*<LS_APOSTROPHE>)> : LS_EXPECTED
    | <LS_SINGLE_STRING : (<LS_CHAR>)+> : LS_EXPECTED
    | <#LS_APOSTROPHE: "\"" >
    | <#LS_CHAR : ~["\u0000"-"\u0020","\"","{","}","\u0100"-"\uffff"]>
}

/************************************************************* String -> List */
<SL_EXPECTED> SKIP :
{
  " " | "\t" | "\n" | "\r"
}

<SL_EXPECTED> TOKEN :
{
      <SL_STRING : (<SL_APOSTROPHE>(<SL_CHAR> | "\\\"" | "\\{" | "\\}" | " ")*<SL_APOSTROPHE>)> : L_EXPECTED
    | <SL_SINGLE_STRING : (<SL_CHAR>)+> : L_EXPECTED
    | <#SL_APOSTROPHE: "\"" >
    | <#SL_CHAR : ~["\u0000"-"\u0020","\"","{","}","\u0100"-"\uffff"]>
}

/******************************************************************** String  */
<S_EXPECTED> SKIP :
{
  " " | "\t" | "\n" | "\r"
}

<S_EXPECTED> TOKEN :
{
      <S_STRING : (<S_APOSTROPHE>(<S_CHAR> | "\\\"" | "\\{" | "\\}" | " ")*<S_APOSTROPHE>)> : DEFAULT
    | <S_SINGLE_STRING : (<S_CHAR>)+> : DEFAULT
    | <#S_APOSTROPHE: "\"" >
    | <#S_CHAR : ~["\u0000"-"\u0020","\"","{","}","\u0100"-"\uffff"]>
}

/**************************************************************** List or All */
<LALL_EXPECTED> SKIP:
{
  " " | "\t" | "\n" | "\r"
}
<LALL_EXPECTED> TOKEN : /* List of Strings */
{
    < LALL_CURLY_BRACKET_OPEN : "{" >    : L2_EXPECTED
  | < LALL_ALL : "all" > : DEFAULT
}

/*********************************************************************** List */
<L_EXPECTED> SKIP:
{
  " " | "\t" | "\n" | "\r"
}
<L_EXPECTED> TOKEN : /* List of Strings */
{
      <L_CURLY_BRACKET_OPEN : "{">    : L2_EXPECTED
}

<L2_EXPECTED> SKIP:
{
  " " | "\t" | "\n" | "\r"
}
<L2_EXPECTED> TOKEN : /* List of Strings */
{
      <L_CURLY_BRACKET_CLOSE : "}">   : DEFAULT
    | <L_STRING : (<L_APOSTROPHE>(<L_CHAR> | "\\\"" | "\\{" | "\\}" | " ")*<L_APOSTROPHE>)> : L2_EXPECTED
    | <L_SINGLE_STRING : (<L_CHAR>)+> : L2_EXPECTED
    | <#L_APOSTROPHE: "\"" >
    | <#L_CHAR : ~["\u0000"-"\u0020","\"","{","}","\u0100"-"\uffff"]>
    | <L2_CURLY_BRACKET_OPEN : "{">    : L3_EXPECTED
}

<L3_EXPECTED> TOKEN : /* List of Strings */
{
      <L3_BRACE_STRING  : (<L3_CHAR>)*> : L4_EXPECTED
    | <#L3_CHAR : ~["\u0000"-"\u001f","{","}","\u0100"-"\uffff"]>
}

<L4_EXPECTED> TOKEN : /* List of Strings */
{
      <L4_CURLY_BRACKET_CLOSE : "}">   : L2_EXPECTED
}

/**
 * Parses one complete policy definition.
 *
 * @param _paramCache   parameter cache
 * @param _typeDef      type definition of the policy (to instantiate the
 *                      policy)
 * @param _mxName       MX name of the policy
 */
Policy_mxJPO policy(final ParameterCache_mxJPO _paramCache,
                    final TypeDef_mxJPO _typeDef,
                    final String _mxName)
    throws SecurityException, IllegalArgumentException, NoSuchMethodException, InstantiationException, IllegalAccessException, InvocationTargetException
: {
    final Policy_mxJPO policy = (Policy_mxJPO) _typeDef.newTypeInstance(_mxName);
    String tmp;
} {
    (
        ( <DESCRIPTION>    tmp = sString()     {this.getField(policy, "description").set(tmp);} )
      | ( format(policy) )
      | ( type(policy) )
      | ( <DEFAULTFORMAT>  tmp = sString()     {this.getField(policy, "defaultFormat").set(tmp);} )
      | ( <ENFORCE>        ( <BOOLEAN_TRUE>    {this.getField(policy, "enforce").set(true);}
                           | <BOOLEAN_FALSE>   {this.getField(policy, "enforce").set(false);} ) )
      | ( <DELIMITER>      tmp = sString()     {this.getField(policy, "delimiter").set(tmp);} )
      | ( <SEQUENCE>       tmp = sString()     {this.getField(policy, "minorsequence").set(tmp);} )
      | ( <MINORSEQUENCE>  tmp = sString()     {this.getField(policy, "minorsequence").set(tmp);} )
      | ( <MAJORSEQUENCE>  tmp = sString()     {this.getField(policy, "majorsequence").set(tmp);} )
      | ( <STORE>          tmp = sString()     {this.getField(policy, "store").set(tmp);} )
      | ( <HIDDEN>         ( <BOOLEAN_TRUE>    {this.getField(policy, "hidden").set(true);}
                           | <BOOLEAN_FALSE>   {this.getField(policy, "hidden").set(false);} ) )
      | ( allstate(policy) )
      | ( state(policy) )
    )*
    {
        this.prepareObject(_paramCache, policy);
        return policy;
    }
}

/**
 * Format of a policy definition.
 *
 * @param _policy   current parsed policy
 */
void format(final Policy_mxJPO _policy) :
{
    Set<String> set;
}
{
    <FORMAT>
    ( LOOKAHEAD(2)
        ( <LALL_ALL> {set = new HashSet<String>();set.add("all");} )
      | ( set = lList() )
    )
    {
        this.getField(_policy, "formats").set(set);
    }
}

/**
 * Format of a type definition.
 *
 * @param _policy   current parsed policy
 */
void type(final Policy_mxJPO _policy) :
{
    Set<String> set;
}
{
    <TYPE>
    ( LOOKAHEAD(2)
        ( <LALL_ALL> {set = new HashSet<String>();set.add("all");} )
      | ( set = lList() )
    )
    {
        this.getField(_policy, "types").set(set);
    }
}

/**
 * Parses the all state definition.
 *
 * @param _policy   policy for which the all state definition is parsed
 */
void allstate(final Policy_mxJPO _policy) : {
    final AccessList_mxJPO accessList = this.getField(_policy, "allStateAccess").<AccessList_mxJPO>get();
} {
    <ALLSTATE>
    "{"
        (
            stateOwnerAccess(Prefix.All, accessList)
          | statePublicAccess(Prefix.All, accessList)
          | stateUserAccess(Prefix.All, accessList)
          | ( <REVOKE> ( stateOwnerAccess(Prefix.Revoke, accessList) | statePublicAccess(Prefix.Revoke, accessList) | stateUserAccess(Prefix.Revoke, accessList) ) )
          | ( <LOGIN>  ( stateOwnerAccess(Prefix.Login,  accessList) | statePublicAccess(Prefix.Login,  accessList) | stateUserAccess(Prefix.Login,  accessList) ) )
        )*
    "}"
    {
        this.getField(_policy, "allState").set(true);
    }
}

/**
 * Parses one state definition.
 *
 * @param _policy   policy for which the all state definition is parsed
 */
void state(final Policy_mxJPO _policy) : {
    final State state = new State();
    String tmpStr;
    Set<String> tmpSet;
} {
    <STATE> tmpStr = sString() {this.getField(state, "name").set(tmpStr);}
    "{"
        (              (<REGISTEREDNAME>     tmpStr = sString()  {this.appendValue(state, "symbolicNames", tmpStr);})
            | (<REVISION>           ( <BOOLEAN_TRUE>    {this.getField(state, "minorrevisionable").set(true);}
                                    | <BOOLEAN_FALSE>   {this.getField(state, "minorrevisionable").set(false);} ) )
            | (<MINORREVISION>      ( <BOOLEAN_TRUE>    {this.getField(state, "minorrevisionable").set(true);}
                                    | <BOOLEAN_FALSE>   {this.getField(state, "minorrevisionable").set(false);} ) )
            | (<MAJORREVISION>      ( <BOOLEAN_TRUE>    {this.getField(state, "majorrevisionable").set(true);}
                                    | <BOOLEAN_FALSE>   {this.getField(state, "majorrevisionable").set(false);} ) )
            | (<VERSION>            ( <BOOLEAN_TRUE>    {this.getField(state, "versionable").set(true);}
                                    | <BOOLEAN_FALSE>   {this.getField(state, "versionable").set(false);} ) )
            | (<PROMOTE>            ( <BOOLEAN_TRUE>    {this.getField(state, "autoPromotion").set(true);}
                                    | <BOOLEAN_FALSE>   {this.getField(state, "autoPromotion").set(false);} ) )
            | (<CHECKOUTHISTORY>    ( <BOOLEAN_TRUE>    {this.getField(state, "checkoutHistory").set(true);}
                                    | <BOOLEAN_FALSE>   {this.getField(state, "checkoutHistory").set(false);} ) )
            | ((<ENFORCERESERVEACCESS> | <ENFORCERESERVEACCESS_TRUE>)
                                                        {this.getField(state, "enforcereserveaccess").set(true);} )
            | ((<ENFORCERESERVEACCESS_NOT> | <ENFORCERESERVEACCESS_EXCLAMATION> | <ENFORCERESERVEACCESS_FALSE>)
                                                        {this.getField(state, "enforcereserveaccess").set(false);} )
            | (<PUBLISHED>          ( <BOOLEAN_TRUE>    {this.getField(state, "published").set(true);}
                                    | <BOOLEAN_FALSE>   {this.getField(state, "published").set(false);} ) )
            | stateOwnerAccess(Prefix.All, state)
            | statePublicAccess(Prefix.All, state)
            | stateUserAccess(Prefix.All, state)
            | (<REVOKE> ( stateOwnerAccess(Prefix.Revoke, state) | statePublicAccess(Prefix.Revoke, state) | stateUserAccess(Prefix.Revoke, state) ) )
            | (<LOGIN>  ( stateOwnerAccess(Prefix.Login,  state) | statePublicAccess(Prefix.Login,  state) | stateUserAccess(Prefix.Login,  state) ) )
            | (<ACTION>             tmpStr = sString()  {this.getField(state, "actionProgram").set(tmpStr);}
               <INPUT>              tmpStr = sString()  {this.getField(state, "actionInput").set(tmpStr);})
            | (<CHECK>              tmpStr = sString()  {this.getField(state, "checkProgram").set(tmpStr);}
               <INPUT>              tmpStr = sString()  {this.getField(state, "checkInput").set(tmpStr);})
            | (<ROUTE>              tmpSet = lsList()   {this.getField(state, "routeUsers").set(tmpSet);}
                                    tmpStr = sString()  {this.getField(state, "routeMessage").set(tmpStr);})
            | stateTrigger(state)
            | stateSignature(state)
            | stateProperty(state)
        )*
    "}"
    {
        this.appendValue(_policy, "states", state);
    }
}

/**
 * Parses the access and filter expression for the owner definition.
 *
 * @param _prefix   access filter prefix
 * @param _access   current parsed access definition of the policy
 */
void stateOwnerAccess(final Prefix _prefix, final AccessList_mxJPO _accessList) : {
    final Access access = new Access();
}  {
    ( <OWNER> stateAccessDef(access) )
    {
        this.getField(access, "prefix").set(_prefix);
        this.getField(access, "kind").set("owner");
        ((Collection<Access>)(this.getValue(_accessList, "accessList"))).add(access);
    }
}

/**
 * Parses the access and filter expression for the public definition.
 *
 * @param _prefix   access filter prefix
 * @param _access   current parsed access definition of the policy
 */
void statePublicAccess(final Prefix _prefix, final AccessList_mxJPO _accessList) : {
    final Access access = new Access();
}  {
    ( <PUBLIC> stateAccessDef(access) )
    {
        this.getField(access, "prefix").set(_prefix);
        this.getField(access, "kind").set("public");
        ((Collection<Access>)(this.getValue(_accessList, "accessList"))).add(access);
    }
}

/**
 * Parses the access and filter expression for a user definition.
 *
 * @param _prefix   access filter prefix
 * @param _access   current parsed access definition of the policy
 */
void stateUserAccess(final Prefix _prefix, final AccessList_mxJPO _accessList) : {
    final Access access = new Access();
    String user;
}  {
    ( <USER> user  = stateAccessString() stateAccessDef(access) )
    {
        this.getField(access, "prefix").set(_prefix);
        this.getField(access, "kind").set("user");
        this.getField(access, "userRef").set(user);
        ((Collection<Access>)(this.getValue(_accessList, "accessList"))).add(access);
    }
}

void stateAccessDef(final Access _access) : {
    String key = null, filter = null, localfilter = null, organization = null, project = null, owner = null, reserve = null, maturity = null, category = null;
    Token access = null;
} {
    (
          ( <KEY> key = stateAccessString())
        | ( <FILTER> filter = sString())
        | ( <LOCALFILTER> localfilter = sString() )
        | ( access = <ACCESS> )
        | ( <ORGANIZATION_ANY>        {organization = "any";        } )
        | ( <ORGANIZATION_SINGLE>     {organization = "single";     } )
        | ( <ORGANIZATION_ANCESTOR>   {organization = "ancestor";   } )
        | ( <ORGANIZATION_DESCENDANT> {organization = "descendant"; } )
        | ( <ORGANIZATION_RELATED>    {organization = "related";    } )
        | ( <PROJECT_ANY>             {project      = "any";        } )
        | ( <PROJECT_SINGLE>          {project      = "single";     } )
        | ( <PROJECT_ANCESTOR>        {project      = "ancestor";   } )
        | ( <PROJECT_DESCENDANT>      {project      = "descendant"; } )
        | ( <PROJECT_RELATED>         {project      = "related";    } )
        | ( <OWNER_ANY>               {owner        = "any";        } )
        | ( <OWNER_CONTEXT>           {owner        = "context";    } )
        | ( <RESERVE_ANY>             {reserve      = "any";        } )
        | ( <RESERVE_CONTEXT>         {reserve      = "context";    } )
        | ( <RESERVE_NO>              {reserve      = "no";         } )
        | ( <RESERVE_INCLUSIVE>       {reserve      = "inclusive";  } )
        | ( <MATURITY_ANY>            {maturity     = "any";        } )
        | ( <MATURITY_NO>             {maturity     = "no";         } )
        | ( <MATURITY_PUBLIC>         {maturity     = "public";     } )
        | ( <MATURITY_PROTECTED>      {maturity     = "protected";  } )
        | ( <MATURITY_PRIVATE>        {maturity     = "private";    } )
        | ( <MATURITY_NOTPRIVATE>     {maturity     = "notprivate"; } )
        | ( <MATURITY_PPP>            {maturity     = "ppp";        } )
        | ( <CATEGORY_ANY>            {category     = "any";         } )
        | ( <CATEGORY_OEM>            {category     = "oem";         } )
        | ( <CATEGORY_GOLDPARTNER>    {category     = "goldpartner"; } )
        | ( <CATEGORY_PARTNER>        {category     = "partner";     } )
        | ( <CATEGORY_SUPPLIER>       {category     = "supplier";    } )
        | ( <CATEGORY_CUSTOMER>       {category     = "customer";    } )
        | ( <CATEGORY_CONTRACTOR>     {category     = "contractor";  } )
    )*
    {
        this.getField(_access, "key").set(key);
        this.getField(_access, "filter").set(filter);
        this.getField(_access, "localfilter").set(localfilter);
        this.getField(_access, "organization").set(organization);
        this.getField(_access, "project").set(project);
        this.getField(_access, "owner").set(owner);
        this.getField(_access, "reserve").set(reserve);
        this.getField(_access, "maturity").set(maturity);
        this.getField(_access, "category").set(category);

        if (access == null)  {
            this.getField(_access, "access").set(new HashSet<String>());
        } else  {
            final String tmp = access.image
                    .replaceFirst("^\\{", "").replaceFirst("\\}$", "")  // remove {}
                    .replaceAll("(\t)|(\n)", " ")                       // replace tabs, new lines
                    .replaceAll("( )+", " ")                            // multiple spaces => one space
                    .trim();                                            // remove trailing spaces
            this.getField(_access, "access").set(new HashSet<String>(Arrays.asList(tmp.split(" "))));
        }
    }
}

/**
 * Evaluates a simple string where the state access definition must follow.
 *
 * @return evaluated simple string
 */
String stateAccessString() : {
    Token tmp;
    String ret;
} {
    (
        (tmp = <STATEACCESS_STRING>)         {ret = this.getString(tmp.image);}
        |
        (tmp = <STATEACCESS_SINGLE_STRING>)  {ret = this.getSingle(tmp.image);}
    )
    {
        return ret;
    }
}

void stateTrigger(final State _state) : {
    final Trigger trigger = new Trigger();
    String tmp;
} {
    <TRIGGER> tmp = sString()       {this.setValue(trigger, "eventType", tmp);}
            ( <ACTION>              {this.setValue(trigger, "kind", "action");}
            | <CHECK>               {this.setValue(trigger, "kind", "check");}
            | <OVERRIDE>            {this.setValue(trigger, "kind", "override");})
            tmp = sString()         {this.setValue(trigger, "program", tmp);}
            <INPUT> tmp = sString() {this.setValue(trigger, "arguments", tmp);}
    {
        this.appendValue(this.getValue(_state, "triggers"), "triggersStack", trigger);
    }
}

void stateSignature(final State _state) : {
    final Policy_mxJPO.Signature signature = new Policy_mxJPO.Signature();
    String tmpStr;
    Set<String> tmpSet;
} {
    <SIGNATURE> tmpStr = sString() {this.getField(signature, "name").set(tmpStr);}
    "{"
        ( (<BRANCH> tmpStr = sString()  {this.getField(signature, "branch").set(tmpStr);})
        | (<APPROVE> tmpSet = lList()   {this.getField(signature, "approverUsers").set(tmpSet);})
        | (<IGNORE> tmpSet = lList()    {this.getField(signature, "ignoreUsers").set(tmpSet);})
        | (<REJECT> tmpSet = lList()    {this.getField(signature, "rejectUsers").set(tmpSet);})
        | (<FILTER> tmpStr = sString()  {this.getField(signature, "filter").set(tmpStr);})) *
    "}"
    {
        this.appendValue(_state, "signatures", signature);
    }
}


/**
 * Parses one property for a state.
 *
 * @param _state    state to append the property
 */
void stateProperty(final State _state) : {
    Token tmp;
    String name = null;
    String value = null;
    String toType = null;
    String toName = null;
}  {
    <STATE_PROPERTY> name = sString()
    (
          ( <STATE_PROPERTYVAL> value = sString() )
        | ( <STATE_PROPERTYTO>
                ((tmp = <STATE_PROPERTYTOTYPE>) {toType = this.getSingle(tmp.image);})
                toName = sString() )
    )*
    {
        final AdminProperty prop = new AdminProperty();
        this.setValue(prop, "name", name);
        this.setValue(prop, "value", value);
        this.setValue(prop, "refAdminType", toType);
        this.setValue(prop, "refAdminName", toName);
        this.appendValue(_state, "properties", prop);
    }
}

/******************************************************************************/

Set<String> lsList() :  {
    Token tmp;
    Set<String> ret = new HashSet<String>();
}  {
    (
        <LS_CURLY_BRACKET_OPEN>
            (
                (tmp = <LS_STRING>)         {ret.add(this.getString(tmp.image));}
                |
                (tmp = <LS_SINGLE_STRING>)  {ret.add(this.getSingle(tmp.image));}
            )*
        <LS_CURLY_BRACKET_CLOSE>
    )
    {
        return ret;
    }
}

Set<String> lList() :  {
    Token tmp;
    Set<String> ret = new HashSet<String>();
}  {
    (
        ( <L_CURLY_BRACKET_OPEN> | <LALL_CURLY_BRACKET_OPEN> )
            (
                (tmp = <L_STRING>) {ret.add(this.getString(tmp.image));}
                |
                (tmp = <L_SINGLE_STRING>) {ret.add(this.getSingle(tmp.image));}
                |
                <L2_CURLY_BRACKET_OPEN>
                    (tmp = <L3_BRACE_STRING>)
                    {ret.add(tmp.image);}
                <L4_CURLY_BRACKET_CLOSE>
            )*
        <L_CURLY_BRACKET_CLOSE>
    )
    {
        return ret;
    }
}

/**
 * Evaluates a simple string where a list must follow.
 *
 * @return evaluated simple string
 */
String slString() : {
    Token tmp;
    String ret;
} {
    (
        (tmp = <SL_STRING>)         {ret = this.getString(tmp.image);}
        |
        (tmp = <SL_SINGLE_STRING>)  {ret = this.getSingle(tmp.image);}
    )
    {
        return ret;
    }
}

/**
 * Evaluates a simple string.
 *
 * @return evaluated simple string
 */
String sString() : {
    Token tmp;
    String ret;
} {
    (
          (tmp = <S_STRING>)          {ret = this.getString(tmp.image);}
        | (tmp = <S_SINGLE_STRING>)   {ret = this.getSingle(tmp.image);}
        | (tmp = <DELIMITER_CHAR>)    {ret = this.getSingle(tmp.image);}
    )
    {
        return ret;
    }
}
